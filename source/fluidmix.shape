##preamble \usepackage{mlsstyle}

##needs ..Shapes..Layout / centering-X
##needs ..Applications..Blockdraw

##lookin ..Shapes
##lookin ..Shapes..Graphics
##lookin ..Shapes..Geometry

##lookin ..Applications..Blockdraw

connectWidth: Traits..@width:0.8bp
solderDot: [Graphics..fill [circle 1.5bp]]
permeableDash: Traits..@dash: [Traits..dashpattern 2bp 2bp]

blue: [Traits..rgb 0.68 0.85 0.87]
red: [Traits..rgb 0.98 0.09 0.03]

box_ry: 7mm
box_len: 30mm
boxPth: [rectangle (0mm,~box_ry) (box_len,box_ry)] >> [shift (box_ry * [Numeric..Math..tan 30°], 0)]

leftBox: \ lbl →
  {
    [[shift (2mm, 0)] [Layout..center_y lbl]]
  } >> [shift (box_ry * [Numeric..Math..tan 30°], 0)]

rightBox: \ lbl →
  {
    [[shift (2mm - box_len, 0)] [Layout..center_y lbl]]
  } >> [shift (~box_ry * [Numeric..Math..tan 30°], 0)]

openBox: [boxPth 0]--[boxPth 2]
border:
  [[Data..range '0 '2].foldl \ p e → p--(openBox >> [rotate e * 120°]) emptypath]--cycle
permeablePth:
  [[Data..range '0 '2].foldl \ p e → p--[[rotate e * 120°] [boxPth 0].p] emptypath]--cycle

IO..•page << Traits..@fill:blue & Traits..@width:0.3bp | [Graphics..fillstroke border]
IO..•page << permeableDash & Traits..@width:0.3bp | [Graphics..stroke permeablePth]
IO..•page << [rightBox [TeX `\begin{minipage}{26mm}\begin{flushright}$m_{\mathrm{flow},1}$, $H_{\mathrm{flow},1}$\\$h_{\mathrm{mix}}$\end{flushright}\end{minipage}´]] >> [rotate 60°]
IO..•page << [rightBox [TeX `\begin{minipage}{26mm}\begin{flushright}$m_{\mathrm{flow},2}$, $H_{\mathrm{flow},2}$\\$h_{\mathrm{mix}}$\end{flushright}\end{minipage}´]] >> [rotate ~60°]
IO..•page << [leftBox [TeX `\begin{minipage}{26mm}$m_{\mathrm{flow},3}$, $H_{\mathrm{flow},3}$\\$h_{\mathrm{mix}}$\end{minipage}´]]

  Traits..@stroke: red
& Traits..@fill: red
& Traits..@width: 0.8bp
|
{
  red_c: (0m,0m)
  lbl_x: box_ry * [Numeric..Math..tan 30°] + 1.8mm
  head: [ShapesArrow width:3bp ...]
  IO..•page << [Graphics..stroke red_c--[[rotate 0°] (lbl_x,~2mm)] head:head]
  IO..•page << [Graphics..stroke red_c--[[rotate 120°] (lbl_x,2mm)] head:head]
  IO..•page << [Graphics..stroke red_c--[[rotate 240°] (lbl_x,2mm)] head:head]
  IO..•page << [Graphics..fill [circle 1.5bp] >> [shift red_c]]
}
